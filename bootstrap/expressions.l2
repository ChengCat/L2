(function -storage- (l r) [mk# $l $r #0])
(function -function- (l r) [mk# $l $r #1])
(function -with- (l r) [mk# $l $r #2])
(function -invoke- (l r) [mk# $l $r #3])
(function -if- (l r) [mk# $l $r #4])
(function -begin- (l r) [mk# $l $r #5])
(function -literal- (l r) [mk# $l $r #6])
(function -reference- (l r) [mk# $l $r #7])
(function -jump- (l r) [mk# $l $r #8])
(function -continuation- (l r) [mk# $l $r #9])
(function -assembly- (l r) [mk# $l $r #10])
(function -meta- (l r) [mk# $l $r #11])

(function static-storage (l r) [mk# $l $r #0])
(function dynamic-storage (l r) [mk# $l $r #1])

(function local-scope (l r) [mk# $l $r #0])
(function global-scope (l r) [mk# $l $r #1])

(function undefined-state (l r) [mk# $l $r #0])
(function defined-state (l r) [mk# $l $r #1])

(function symbol (l r) [mk# $l $r #48])

(function sym-name (l r) [mk-field $l $r #0 #8])
(function sym-offset (l r) [mk-field $l $r #8 #8])
(function sym-size (l r) [mk-field $l $r #16 #8])
(function sym-definition (l r) [mk-field $l $r #24 #8])
(function sym-context (l r) [mk-field $l $r #32 #8])
(function sym-type (l r) [mk-field $l $r #40 #1])
(function sym-scope (l r) [mk-field $l $r #41 #1])
(function sym-state (l r) [mk-field $l $r #42 #1])

(function make-symbol (type scope state name definition r) (with return
	(let (sym [buffer-alloc $r (symbol)]) (begin
		(setf sym-type $sym $type)
		(setf sym-scope $sym $scope)
		(setf sym-state $sym $state)
		(setf sym-name $sym $name)
		(setf sym-definition $sym $definition)
		{return $sym}))))

(function expression (l r) [mk# $l $r #64])

(function expr-type (l r) [mk-field $l $r #0 #8])
(function expr-parent (l r) [mk-field $l $r #8 #8])
(function expr-return-symbol (l r) [mk-field $l $r #16 #8])
(++ Only for type(s) (-begin-))
(function expr-expressions (l r) [mk-field $l $r #24 #8])
(++ Only for type(s) (-assembly-))
(function expr-opcode (l r) [mk-field $l $r #24 #8])
(++ Only for type(s) (-assembly-) (-storage-) (-invoke-) (-jump-))
(function expr-arguments (l r) [mk-field $l $r #32 #8])
(++ Only for type(s) (-storage-) (-invoke-) (-jump-) (-function-) (-continuation-) (-with-) (-meta-))
(function expr-reference (l r) [mk-field $l $r #24 #8])
(++ Only for type(s) (-jump-))
(function expr-short-circuit (l r) [mk-field $l $r #40 #8])
(++ Only for type(s) (-if-))
(function expr-condition (l r) [mk-field $l $r #24 #8])
(function expr-consequent (l r) [mk-field $l $r #32 #8])
(function expr-alternate (l r) [mk-field $l $r #40 #8])
(++ Only for type(s) (-literal-))
(function expr-value (l r) [mk-field $l $r #24 #8])
(++ Only for type(s) (-function-) (-continuation-) (-with-))
(function expr-expression (l r) [mk-field $l $r #32 #8])
(function expr-parameters (l r) [mk-field $l $r #40 #8])
(++ Only for type(s) (-function-))
(function expr-symbols (l r) [mk-field $l $r #48 #8])
(function expr-expression-return-symbol (l r) [mk-field $l $r #56 #8])
(++ Only for type(s) (-continuation-) (-with-))
(function expr-cont-instr-ref (l r) [mk-field $l $r #48 #8])
(function expr-escapes (l r) [mk-field $l $r #56 #8])
(++ Only for type(s) (-reference-))
(function expr-name (l r) [mk-field $l $r #24 #8])
(function expr-symbol (l r) [mk-field $l $r #32 #8])
(++ Only for type(s) (-meta-))
(function expr-argument (l r) [mk-field $l $r #32 #8])

(function make-literal (value reg) (with return
	(let (t [buffer-alloc $reg (expression)]) (begin
		(setf expr-type $t (-literal-))
		(setf expr-value $t $value)
		{return $t}))))

(function make-reference (name reg) (with return
	(let (ref [buffer-alloc $reg (expression)]) (begin
		(setf expr-type $ref (-reference-))
		(setf expr-name $ref $name)
		(setf expr-symbol $ref (null))
		{return $ref}))))

(function refer-reference (reference referent) (setf expr-symbol $reference (@ expr-symbol $referent)))

(function use-symbol (sym reg) (with return
	(let (ref [buffer-alloc $reg (expression)]) (begin
		(setf expr-type $ref (-reference-))
		(setf expr-name $ref (@ sym-name $sym))
		(setf expr-symbol $ref $sym)
		{return $ref}))))

(function make-begin (expressions reg) (with return
	(let (beg [buffer-alloc $reg (expression)]) (begin
		(setf expr-type $beg (-begin-))
		(setf expr-expressions $beg $expressions)
		(foreach (expr $expressions) (setf expr-parent $expr $beg))
		{return $beg}))))

(function make-function (ref params expr reg) (with return
	(let (func [buffer-alloc $reg (expression)]) (begin
		(setf expr-type $func (-function-))
		(setf expr-reference $func $ref)
		(setf expr-parent $ref $func)
		(setf expr-symbol $ref [make-symbol (static-storage) (local-scope) (defined-state) (@ expr-name $ref) $ref $reg])
		(setf expr-parameters $func $params)
		(foreach (param $params) (begin
			(setf expr-parent $param $func)
			(setf expr-symbol $param [make-symbol (dynamic-storage) (local-scope) (defined-state) (@ expr-name $param) $param $reg])))
		(setf expr-symbols $func nil)
		(setf expr-expression $func $expr)
		(setf expr-parent $expr $func)
		{return $func}))))

(function make-functionN (l r)
	(`[make-function (,[@fst $l])
		(,[list [meta:reverse [lst [@frrrst $l] [meta:reverse [@frst $l] $r] $r] $r] $r emt]) (,[@frrst $l]) (,[@frrrst $l])]$r))

(function make-continuation (ref params expr reg) (with return
	(let (cont [buffer-alloc $reg (expression)]) (begin
		(setf expr-type $cont (-continuation-))
		(setf expr-reference $cont $ref)
		(setf expr-parent $ref $cont)
		(setf expr-symbol $ref [make-symbol (dynamic-storage) (local-scope) (defined-state) (@ expr-name $ref) $ref $reg])
		(setf expr-parameters $cont $params)
		(foreach (param $params) (begin
			(setf expr-parent $param $cont)
			(setf expr-symbol $param [make-symbol (dynamic-storage) (local-scope) (defined-state) (@ expr-name $param) $param
				$reg])))
		(setf expr-escapes $cont (true))
		(setf expr-expression $cont $expr)
		(setf expr-parent $expr $cont)
		{return $cont}))))

(function make-continuationN (l r)
	(`[make-continuation (,[@fst $l])
		(,[list [meta:reverse [lst [@frrrst $l] [meta:reverse [@frst $l] $r] $r] $r] $r emt]) (,[@frrst $l]) (,[@frrrst $l])]$r))

(function make-with (ref expr reg) (with return
	(let (wth [buffer-alloc $reg (expression)]) (begin
		(setf expr-type $wth (-with-))
		(setf expr-reference $wth $ref)
		(setf expr-parent $ref $wth)
		(setf expr-symbol $ref [make-symbol (dynamic-storage) (local-scope) (defined-state) (@ expr-name $ref) $ref $reg])
		(let (param [make-reference (null) $reg]) (begin
			(setf expr-parent $param $wth)
			(setf expr-symbol $param [make-symbol (dynamic-storage) (local-scope) (defined-state) (@ expr-name $param) $param
				$reg])
			(setf expr-parameters $wth [lst $param nil $reg])))
		(setf expr-escapes $wth (true))
		(setf expr-expression $wth $expr)
		(setf expr-parent $expr $wth)
		{return $wth}))))

(function make-asm (opcode arguments reg) (with return
	(let (u [buffer-alloc $reg (expression)]) (begin
		(setf expr-type $u (-assembly-))
		(setf expr-opcode $u $opcode)
		(setf expr-arguments $u $arguments)
		(foreach (arg $arguments) (setf expr-parent $arg $u))
		{return $u}))))

(function make-asmN (l r)
	(`[make-asm (,[@fst $l]) (,[list [meta:reverse [lst [@frrst $l] [meta:reverse [@frst $l] $r] $r] $r] $r emt])
		(,[@frrst $l])]$r))

(function make-jump (ref args reg) (with return
	(let (u [buffer-alloc $reg (expression)]) (begin
		(setf expr-type $u (-jump-))
		(setf expr-reference $u $ref)
		(setf expr-parent $ref $u)
		(setf expr-arguments $u $args)
		(foreach (arg $args) (setf expr-parent $arg $u))
		{return $u}))))

(function make-jumpN (l r)
	(`[make-jump (,[@fst $l]) (,[list [meta:reverse [lst [@frrst $l] [meta:reverse [@frst $l] $r] $r] $r] $r emt])
		(,[@frrst $l])]$r))

(function make-storage (ref args reg) (with return
	(let (u [buffer-alloc $reg (expression)]) (begin
		(setf expr-type $u (-storage-))
		(setf expr-reference $u $ref)
		(setf expr-parent $ref $u)
		(setf expr-symbol $ref [make-symbol (dynamic-storage) (local-scope) (defined-state) (@ expr-name $ref) $ref $reg])
		(setf expr-arguments $u $args)
		(foreach (arg $args) (setf expr-parent $arg $u))
		{return $u}))))

(function make-meta (ref arg reg) (with return
	(let (u [buffer-alloc $reg (expression)]) (begin
		(setf expr-type $u (-meta-))
		(setf expr-reference $u $ref)
		(setf expr-parent $ref $u)
		(setf expr-argument $u $arg)
		{return $u}))))

(function make-if (condition consequent alternate reg) (with return
	(let (u [buffer-alloc $reg (expression)]) (begin
		(setf expr-type $u (-if-))
		(setf expr-condition $u $condition)
		(setf expr-parent $condition $u)
		(setf expr-consequent $u $consequent)
		(setf expr-parent $consequent $u)
		(setf expr-alternate $u $alternate)
		(setf expr-parent $alternate $u)
		{return $u}))))

(function make-invoke (ref args reg) (with return
	(let (u [buffer-alloc $reg (expression)]) (begin
		(setf expr-type $u (-invoke-))
		(setf expr-reference $u $ref)
		(setf expr-parent $ref $u)
		(setf expr-arguments $u $args)
		(foreach (arg $args) (setf expr-parent $arg $u))
		{return $u}))))

(function make-invokeN (l r)
	(`[make-invoke (,[@fst $l]) (,[list [meta:reverse [lst [@frrst $l] [meta:reverse [@frst $l] $r] $r] $r] $r emt])
		(,[@frrst $l])]$r))

(function make-program (exprs r) (with return
	(let (program [make-function [make-reference (null) $r] nil [make-begin $exprs $r] $r]) (begin
		(setf expr-parent $program (null))
		(foreach (expr $exprs)
			(if (or [= (@ expr-type $expr) (-function-)] [= (@ expr-type $expr) (-storage-)])
				(setf sym-scope (@ expr-symbol(@ expr-reference $expr)) (global-scope))
				(begin)))
		{return $program}))))

(function replace-expression (l r)
	(`(let (replace-expression:orig (,[@fst $l]))
		(let (replace-expression:parent (@ expr-parent $$replace-expression:orig))
			(let (replace-expression:repl (,[@frst $l])) (begin
				(setf expr-parent $replace-expression:repl $replace-expression:parent)
				[set $replace-expression:orig $replace-expression:repl]))))$r))

(function put (l r)
	(`(let (put:parent (,[@frst $l])) (put:repl (,[@frrst $l])) (begin
		(setf (,[@fst $l]) $put:parent $put:repl)
		(setf expr-parent $put:repl $put:parent)))$r))

(function print-expression (s)
	(switch = (@ expr-type $s)
		((-begin-) (begin
			[write-str (stdout) (" (lparen)begin(space))]
			(foreach (t (@ expr-expressions $s)) (begin
				[print-expression $t]
				[write-str (stdout) (" (space))]))
			[write-str (stdout) (" (bs)(rparen))]))
		
		((-with-) (begin
			[write-str (stdout) (" (lparen)with(space))]
			[print-expression (@ expr-reference $s)]
			[write-str (stdout) (" (space))]
			[print-expression (@ expr-expression $s)]
			[write-str (stdout) (" (rparen))]))
		
		((-invoke-) (-jump-) (-storage-) (begin
			[write-str (stdout) (switch = (@ expr-type $s)
				((-invoke-) (" (lbracket)))
				((-jump-) (" (lbrace)))
				(" (lparen)storage(space)))]
			[print-expression (@ expr-reference $s)]
			[write-str (stdout) (" (space))]
			(foreach (t (@ expr-arguments $s)) (begin
				[print-expression $t]
				[write-str (stdout) (" (space))]))
			[write-str (stdout) (" (bs))]
			[write-str (stdout) (switch = (@ expr-type $s)
				((-invoke-) (" (rbracket)))
				((-jump-) (" (rbrace)))
				(" (rbracket)))]))
		
		((-function-) (-continuation-) (begin
			[write-str (stdout) (" (lparen))]
			[write-str (stdout) (switch = (@ expr-type $s)
				((-function-) (" function))
				(" continuation))]
			[write-str (stdout) (" (space))]
			[print-expression (@ expr-reference $s)]
			[write-str (stdout) (" (space)(lparen)(space))]
			(foreach (t (@ expr-parameters $s)) (begin
				[print-expression $t]
				[write-str (stdout) (" (space))]))
			[write-str (stdout) (" (rparen)(space))]
			[print-expression (@ expr-expression $s)]
			[write-str (stdout) (" (rparen))]))
			
		((-if-) (begin
			[write-str (stdout) (" (lparen)if(space))]
			[print-expression (@ expr-condition $s)]
			[write-str (stdout) (" (space))]
			[print-expression (@ expr-consequent $s)]
			[write-str (stdout) (" (space))]
			[print-expression (@ expr-alternate $s)]
			[write-str (stdout) (" (rparen))]))
		
		((-reference-)
			(if (@ expr-name $s)
				[write-str (stdout) (@ expr-name $s)]
				(begin
					[write-str (stdout) (" (lparen)reference(space))]
					[write-uint (stdout) (@ expr-symbol $s)]
					[write-str (stdout) (" (rparen))])))
					
		((-literal-) (begin
			[write-str (stdout) (" (lparen)literal(space))]
			[write-uint (stdout) (@ expr-value $s)]
			[write-str (stdout) (" (rparen))]))
		
		((-meta-) (begin
			[write-str (stdout) (" (lparen))]
			[print-expression (@ expr-reference $s)]
			[write-str (stdout) (" (space))]
			[print-fragment (@ expr-argument $s)]
			[write-str (stdout) (" (rparen))]))
			
		(begin)))

(function build-expression-literal-len (l r) [mk# $l $r #64])

(storage special-form-error #0)

(function build-expression (d reg handler)
	(if [= [length $d] #0] {$handler}
	(if [i/f:symbol? $d] [make-reference [to-string $d $reg] $reg]
	(switch str= [to-string [@car $d] $reg]
		((" with)
			(if (not [= [length $d] #3])
				{$handler special-form-error $d (null)}
			(if (not [i/f:symbol? [@cadr $d]])
				{$handler special-form-error $d [@cadr $d]}
				[make-with [build-expression [@cadr $d] $reg $handler] [build-expression [@caddr $d] $reg $handler] $reg])))
		
		((" begin)
			(with return (let (exprs nil) (begin
				(foreach (v [@cdr $d]) [append [build-expression $v $reg $handler] exprs $reg])
				{return [make-begin $exprs $reg]}))))
		
		((" if)
			(if (not [= [length $d] #4])
				{$handler special-form-error $d (null)}
				[make-if [build-expression [@cadr $d] $reg $handler] [build-expression [@caddr $d] $reg $handler]
					[build-expression [@cadddr $d] $reg $handler] $reg]))
		
		((" function) (" continuation)
			(if (not [= [length $d] #4])
				{$handler special-form-error $d (null)}
			(if (not [i/f:symbol? [@cadr $d]])
				{$handler special-form-error $d [@cadr $d]}
			(if [i/f:symbol? [@caddr $d]]
				{$handler special-form-error $d [@caddr $d]}
				(with return (let (parameters nil) (begin
					(foreach (v [@caddr $d])
						(if [i/f:symbol? $v]
							[append [build-expression $v $reg $handler] parameters $reg]
							{$handler special-form-error $d $v}))
					{return [(switch str= [to-string [@car $d] $reg]
							((" function) make-function)
							make-continuation)
						[build-expression [@cadr $d] $reg $handler] $parameters [build-expression [@cadddr $d] $reg $handler]
							$reg]})))))))
		
		((" invoke) (" jump) (" storage)
			(if [= [length $d] #1]
				{$handler special-form-error $d (null)}
			(if (and [str= [to-string [@car $d] $reg] (" storage)] (not [i/f:symbol? [@cadr $d]]))
				{$handler special-form-error $d [@cadr $d]}
				(with return (let (arguments nil) (begin
					(foreach (v [@cddr $d]) [append [build-expression $v $reg $handler] arguments $reg])
					{return [(switch str= [to-string [@car $d] $reg]
							((" invoke) make-invoke)
							((" jump) make-jump)
							make-storage)
						[build-expression [@cadr $d] $reg $handler] $arguments $reg]}))))))
		
		((" literal)
			(if (not [= [length $d] #2])
				{$handler special-form-error $d (null)}
			(if (or (not [i/f:symbol? [@cadr $d]]) (not [= [length [@cadr $d]] (build-expression-literal-len)]))
				{$handler special-form-error $d [@cadr $d]}
				[make-literal (with return
					{(continuation loop (str i value) (begin
							[set value [<< $value #1]]
							(if [= $i #0]
								{return $value}
							(if [= [getb $str] (char 1)]
								{loop [+ $str #1] [- $i #1] [+ $value #1]}
							(if [= [getb $str] (char 0)]
								{loop [+ $str #1] [- $i #1] $value}
								{$handler special-form-error $d [@cadr $d]})))))
						[to-string [@cadr $d] $reg] (build-expression-literal-len) #0}) $reg])))
		
		[make-meta [build-expression [@car $d] $reg $handler] [@cdr $d] $reg]))))

(let (r [create-buffer #0]) [print-expression [make-begin [llst [make-reference (" Hellodude) $r] [make-literal #20 $r] nil $r] $r]])

[write-str (stdout) (" Starting here:(lf))]

(++(let (r [create-buffer #0]) (pos #0)
	(do-let (begin
			[write-str (stdout) (" (lparen)begin(lbrace)lokay(rbrace)a(rparen)o)]
			[print-expression [build-expression
				[build-fragment (" (lparen)with(lbrace)lokay(rbrace)a(rparen)o) #22 pos $r $handler] $r $handler]]
			[build-symbol (" Hello) $r])
		(handler (continuation _ (code arg0 arg1 arg2 arg3) (begin
			[destroy-buffer $r]
			(switch = $code
				(special-form-error [write-str (stdout) (" Okay, it's a special form error(lf))])
				(unexpected-char-error [write-str (stdout) (" It was an unexpected character!(lf))])
				[write-str (stdout) (" We do not know what caused this!(lf))])
			[exit #1]))))))
