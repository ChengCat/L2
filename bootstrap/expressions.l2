(function -storage- (l r) [mk# $r #0])
(function -function- (l r) [mk# $r #1])
(function -with- (l r) [mk# $r #2])
(function -invoke- (l r) [mk# $r #3])
(function -if- (l r) [mk# $r #4])
(function -begin- (l r) [mk# $r #5])
(function -literal- (l r) [mk# $r #6])
(function -reference- (l r) [mk# $r #7])
(function -jump- (l r) [mk# $r #8])
(function -continuation- (l r) [mk# $r #9])
(function -assembly- (l r) [mk# $r #10])
(function -meta- (l r) [mk# $r #11])

(function static-storage (l r) [mk# $r #0])
(function dynamic-storage (l r) [mk# $r #1])

(function local-scope (l r) [mk# $r #0])
(function global-scope (l r) [mk# $r #1])

(function undefined-state (l r) [mk# $r #0])
(function defined-state (l r) [mk# $r #1])

(function symbol (l r) [mk# $r #48])

(function sym-name (l r) [mk-field $l $r #0 #8])
(function sym-offset (l r) [mk-field $l $r #8 #8])
(function sym-size (l r) [mk-field $l $r #16 #8])
(function sym-definition (l r) [mk-field $l $r #24 #8])
(function sym-context (l r) [mk-field $l $r #32 #8])
(function sym-type (l r) [mk-field $l $r #40 #1])
(function sym-scope (l r) [mk-field $l $r #41 #1])
(function sym-state (l r) [mk-field $l $r #42 #1])

(function make-symbol (type scope state name definition r) (with return
	(let (sym [buffer-alloc $r (symbol)]) (begin
		(setf sym-type $sym $type)
		(setf sym-scope $sym $scope)
		(setf sym-state $sym $state)
		(setf sym-name $sym $name)
		(setf sym-definition $sym $definition)
		{return $sym}))))

(function expression (l r) [mk# $r #64])

(function expr-type (l r) [mk-field $l $r #0 #8])
(function expr-parent (l r) [mk-field $l $r #8 #8])
(function expr-return-symbol (l r) [mk-field $l $r #16 #8])
(;; Only for type(s) (-begin-))
(function expr-expressions (l r) [mk-field $l $r #24 #8])
(;; Only for type(s) (-assembly-))
(function expr-opcode (l r) [mk-field $l $r #24 #8])
(;; Only for type(s) (-assembly-) (-storage-) (-invoke-) (-jump-))
(function expr-arguments (l r) [mk-field $l $r #32 #8])
(;; Only for type(s) (-storage-) (-invoke-) (-jump-) (-function-) (-continuation-) (-with-) (-meta-))
(function expr-reference (l r) [mk-field $l $r #24 #8])
(;; Only for type(s) (-jump-))
(function expr-short-circuit (l r) [mk-field $l $r #40 #8])
(;; Only for type(s) (-if-))
(function expr-condition (l r) [mk-field $l $r #24 #8])
(function expr-consequent (l r) [mk-field $l $r #32 #8])
(function expr-alternate (l r) [mk-field $l $r #40 #8])
(;; Only for type(s) (-literal-))
(function expr-value (l r) [mk-field $l $r #24 #8])
(;; Only for type(s) (-function-) (-continuation-) (-with-))
(function expr-expression (l r) [mk-field $l $r #32 #8])
(function expr-parameters (l r) [mk-field $l $r #40 #8])
(;; Only for type(s) (-function-))
(function expr-symbols (l r) [mk-field $l $r #48 #8])
(function expr-expression-return-symbol (l r) [mk-field $l $r #56 #8])
(;; Only for type(s) (-continuation-) (-with-))
(function expr-cont-instr-ref (l r) [mk-field $l $r #48 #8])
(function expr-escapes (l r) [mk-field $l $r #56 #8])
(;; Only for type(s) (-reference-))
(function expr-name (l r) [mk-field $l $r #24 #8])
(function expr-symbol (l r) [mk-field $l $r #32 #8])
(;; Only for type(s) (-meta-))
(function expr-argument (l r) [mk-field $l $r #32 #8])

(function make-literal (value reg) (with return
	(let (t [buffer-alloc $reg (expression)]) (begin
		(setf expr-type $t (-literal-))
		(setf expr-value $t $value)
		{return $t}))))

(function make-reference (name reg) (with return
	(let (ref [buffer-alloc $reg (expression)]) (begin
		(setf expr-type $ref (-reference-))
		(setf expr-name $ref $name)
		(setf expr-symbol $ref (null))
		{return $ref}))))

(function refer-reference (reference referent) (setf expr-symbol $reference (@ expr-symbol $referent)))

(function use-symbol (sym reg) (with return
	(let (ref [buffer-alloc $reg (expression)]) (begin
		(setf expr-type $ref (-reference-))
		(setf expr-name $ref (@ sym-name $sym))
		(setf expr-symbol $ref $sym)
		{return $ref}))))

(function make-begin (expressions reg) (with return
	(let (beg [buffer-alloc $reg (expression)]) (begin
		(setf expr-type $beg (-begin-))
		(setf expr-expressions $beg $expressions)
		(foreach (expr $expressions) (setf expr-parent $expr $beg))
		{return $beg}))))

(function make-function (ref params expr reg) (with return
	(let (func [buffer-alloc $reg (expression)]) (begin
		(setf expr-type $func (-function-))
		(setf expr-reference $func $ref)
		(setf expr-parent $ref $func)
		(setf expr-symbol $ref [make-symbol (static-storage) (local-scope) (defined-state) (@ expr-name $ref) $ref $reg])
		(setf expr-parameters $func $params)
		(foreach (param $params) (begin
			(setf expr-parent $param $func)
			(setf expr-symbol $param [make-symbol (dynamic-storage) (local-scope) (defined-state) (@ expr-name $param) $param $reg])))
		(setf expr-symbols $func nil)
		(setf expr-expression $func $expr)
		(setf expr-parent $expr $func)
		{return $func}))))

(function make-functionN (l r)
	(`[make-function (,[@fst $l])
		(,[list [meta:reverse [lst [@frrrst $l] [meta:reverse [@frst $l] $r] $r] $r] $r emt]) (,[@frrst $l]) (,[@frrrst $l])]$r))

(function make-continuation (ref params expr reg) (with return
	(let (cont [buffer-alloc $reg (expression)]) (begin
		(setf expr-type $cont (-continuation-))
		(setf expr-reference $cont $ref)
		(setf expr-parent $ref $cont)
		(setf expr-symbol $ref [make-symbol (dynamic-storage) (local-scope) (defined-state) (@ expr-name $ref) $ref $reg])
		(setf expr-parameters $cont $params)
		(foreach (param $params) (begin
			(setf expr-parent $param $cont)
			(setf expr-symbol $param [make-symbol (dynamic-storage) (local-scope) (defined-state) (@ expr-name $param) $param
				$reg])))
		(setf expr-escapes $cont (true))
		(setf expr-expression $cont $expr)
		(setf expr-parent $expr $cont)
		{return $cont}))))

(function make-continuationN (l r)
	(`[make-continuation (,[@fst $l])
		(,[list [meta:reverse [lst [@frrrst $l] [meta:reverse [@frst $l] $r] $r] $r] $r emt]) (,[@frrst $l]) (,[@frrrst $l])]$r))

(function make-with (ref expr reg) (with return
	(let (wth [buffer-alloc $reg (expression)]) (begin
		(setf expr-type $wth (-with-))
		(setf expr-reference $wth $ref)
		(setf expr-parent $ref $wth)
		(setf expr-symbol $ref [make-symbol (dynamic-storage) (local-scope) (defined-state) (@ expr-name $ref) $ref $reg])
		(let (param [make-reference (null) $reg]) (begin
			(setf expr-parent $param $wth)
			(setf expr-symbol $param [make-symbol (dynamic-storage) (local-scope) (defined-state) (@ expr-name $param) $param
				$reg])
			(setf expr-parameters $wth [lst $param nil $reg])))
		(setf expr-escapes $wth (true))
		(setf expr-expression $wth $expr)
		(setf expr-parent $expr $wth)
		{return $wth}))))

(function make-asm (opcode arguments reg) (with return
	(let (u [buffer-alloc $reg (expression)]) (begin
		(setf expr-type $u (-assembly-))
		(setf expr-opcode $u $opcode)
		(setf expr-arguments $u $arguments)
		(foreach (arg $arguments) (setf expr-parent $arg $u))
		{return $u}))))

(function make-asmN (l r)
	(`[make-asm (,[@fst $l]) (,[list [meta:reverse [lst [@frrst $l] [meta:reverse [@frst $l] $r] $r] $r] $r emt])
		(,[@frrst $l])]$r))

(function make-jump (ref args reg) (with return
	(let (u [buffer-alloc $reg (expression)]) (begin
		(setf expr-type $u (-jump-))
		(setf expr-reference $u $ref)
		(setf expr-parent $ref $u)
		(setf expr-arguments $u $args)
		(foreach (arg $args) (setf expr-parent $arg $u))
		{return $u}))))

(function make-jumpN (l r)
	(`[make-jump (,[@fst $l]) (,[list [meta:reverse [lst [@frrst $l] [meta:reverse [@frst $l] $r] $r] $r] $r emt])
		(,[@frrst $l])]$r))

(function make-storage (ref args reg) (with return
	(let (u [buffer-alloc $reg (expression)]) (begin
		(setf expr-type $u (-storage-))
		(setf expr-reference $u $ref)
		(setf expr-parent $ref $u)
		(setf expr-symbol $ref [make-symbol (dynamic-storage) (local-scope) (defined-state) (@ expr-name $ref) $ref $reg])
		(setf expr-arguments $u $args)
		(foreach (arg $args) (setf expr-parent $arg $u))
		{return $u}))))

(function make-meta (ref arg reg) (with return
	(let (u [buffer-alloc $reg (expression)]) (begin
		(setf expr-type $u (-meta-))
		(setf expr-reference $u $ref)
		(setf expr-parent $ref $u)
		(setf expr-argument $u $arg)
		{return $u}))))

(function make-if (condition consequent alternate reg) (with return
	(let (u [buffer-alloc $reg (expression)]) (begin
		(setf expr-type $u (-if-))
		(setf expr-condition $u $condition)
		(setf expr-parent $condition $u)
		(setf expr-consequent $u $consequent)
		(setf expr-parent $consequent $u)
		(setf expr-alternate $u $alternate)
		(setf expr-parent $alternate $u)
		{return $u}))))

(function make-invoke (ref args reg) (with return
	(let (u [buffer-alloc $reg (expression)]) (begin
		(setf expr-type $u (-invoke-))
		(setf expr-reference $u $ref)
		(setf expr-parent $ref $u)
		(setf expr-arguments $u $args)
		(foreach (arg $args) (setf expr-parent $arg $u))
		{return $u}))))

(function make-invokeN (l r)
	(`[make-invoke (,[@fst $l]) (,[list [meta:reverse [lst [@frrst $l] [meta:reverse [@frst $l] $r] $r] $r] $r emt])
		(,[@frrst $l])]$r))

(function make-program (exprs r) (with return
	(let (program [make-function [make-reference (null) $r] nil [make-begin $exprs $r] $r]) (begin
		(setf expr-parent $program (null))
		(foreach (expr $exprs)
			(if (or [= (@ expr-type $expr) (-function-)] [= (@ expr-type $expr) (-storage-)])
				(setf sym-scope (@ expr-symbol(@ expr-reference $expr)) (global-scope))
				(begin)))
		{return $program}))))

(function replace-expression (l r)
	(`(let (replace-expression:orig (,[@fst $l]))
		(let (replace-expression:parent (@ expr-parent $$replace-expression:orig))
			(let (replace-expression:repl (,[@frst $l])) (begin
				(setf expr-parent $replace-expression:repl $replace-expression:parent)
				[set $replace-expression:orig $replace-expression:repl]))))$r))

(function put (l r)
	(`(let (put:parent (,[@frst $l])) (put:repl (,[@frrst $l])) (begin
		(setf (,[@fst $l]) $put:parent $put:repl)
		(setf expr-parent $put:repl $put:parent)))$r))

(function print-expression (s hdl err)
	(switch = (@ expr-type $s)
		((-begin-) (begin
			[print-str (stdout) (" (lparen)begin(space)) $hdl $err]
			(foreach (t (@ expr-expressions $s)) (begin
				[print-expression $t $hdl $err]
				[print-str (stdout) (" (space)) $hdl $err]))
			[print-str (stdout) (" (bs)(rparen)) $hdl $err]))
		
		((-with-) (begin
			[print-str (stdout) (" (lparen)with(space)) $hdl $err]
			[print-expression (@ expr-reference $s) $hdl $err]
			[print-str (stdout) (" (space)) $hdl $err]
			[print-expression (@ expr-expression $s) $hdl $err]
			[print-str (stdout) (" (rparen)) $hdl $err]))
		
		((-invoke-) (-jump-) (-storage-) (begin
			[print-str (stdout) (switch = (@ expr-type $s)
				((-invoke-) (" (lbracket)))
				((-jump-) (" (lbrace)))
				(" (lparen)storage(space))) $hdl $err]
			[print-expression (@ expr-reference $s) $hdl $err]
			[print-str (stdout) (" (space)) $hdl $err]
			(foreach (t (@ expr-arguments $s)) (begin
				[print-expression $t $hdl $err]
				[print-str (stdout) (" (space)) $hdl $err]))
			[print-str (stdout) (" (bs)) $hdl $err]
			[print-str (stdout) (switch = (@ expr-type $s)
				((-invoke-) (" (rbracket)))
				((-jump-) (" (rbrace)))
				(" (rbracket))) $hdl $err]))
		
		((-function-) (-continuation-) (begin
			[print-str (stdout) (" (lparen)) $hdl $err]
			[print-str (stdout) (switch = (@ expr-type $s)
				((-function-) (" function))
				(" continuation)) $hdl $err]
			[print-str (stdout) (" (space)) $hdl $err]
			[print-expression (@ expr-reference $s) $hdl $err]
			[print-str (stdout) (" (space)(lparen)(space)) $hdl $err]
			(foreach (t (@ expr-parameters $s)) (begin
				[print-expression $t $hdl $err]
				[print-str (stdout) (" (space)) $hdl $err]))
			[print-str (stdout) (" (rparen)(space)) $hdl $err]
			[print-expression (@ expr-expression $s) $hdl $err]
			[print-str (stdout) (" (rparen)) $hdl $err]))
			
		((-if-) (begin
			[print-str (stdout) (" (lparen)if(space)) $hdl $err]
			[print-expression (@ expr-condition $s) $hdl $err]
			[print-str (stdout) (" (space)) $hdl $err]
			[print-expression (@ expr-consequent $s) $hdl $err]
			[print-str (stdout) (" (space)) $hdl $err]
			[print-expression (@ expr-alternate $s) $hdl $err]
			[print-str (stdout) (" (rparen)) $hdl $err]))
		
		((-reference-)
			(if (@ expr-name $s)
				[print-str (stdout) (@ expr-name $s) $hdl $err]
				(begin
					[print-str (stdout) (" (lparen)reference(space)) $hdl $err]
					[print-uint (stdout) (@ expr-symbol $s) $hdl $err]
					[print-str (stdout) (" (rparen)) $hdl $err])))
					
		((-literal-) (begin
			[print-str (stdout) (" (lparen)literal(space)) $hdl $err]
			[print-uint (stdout) (@ expr-value $s) $hdl $err]
			[print-str (stdout) (" (rparen)) $hdl $err]))
		
		((-meta-) (begin
			[print-str (stdout) (" (lparen)) $hdl $err]
			[print-expression (@ expr-reference $s) $hdl $err]
			[print-str (stdout) (" (space)) $hdl $err]
			[print-fragment (@ expr-argument $s) $hdl $err]
			[print-str (stdout) (" (rparen)) $hdl $err]))
			
		(begin)))

(function build-expression-literal-len (l r) [mk# $r #64])

(storage special-form-error #0)

(function build-expression (d reg handler err)
	(if [= [length $d] #0] {$handler}
	(if [i/f:symbol? $d] [make-reference [to-string $d $reg] $reg]
	(switch str= [to-string [@car $d] $reg]
		((" with)
			(if (not [= [length $d] #3])
				{$handler (set-storage $err special-form-error $d (null))}
			(if (not [i/f:symbol? [@cadr $d]])
				{$handler (set-storage $err special-form-error $d [@cadr $d])}
				[make-with [build-expression [@cadr $d] $reg $handler $err] [build-expression [@caddr $d] $reg $handler $err] $reg])))
		
		((" begin)
			(with return (let (exprs nil) (begin
				(foreach (v [@cdr $d]) [append [build-expression $v $reg $handler $err] exprs $reg])
				{return [make-begin $exprs $reg]}))))
		
		((" if)
			(if (not [= [length $d] #4])
				{$handler (set-storage $err special-form-error $d (null))}
				[make-if [build-expression [@cadr $d] $reg $handler $err] [build-expression [@caddr $d] $reg $handler $err]
					[build-expression [@cadddr $d] $reg $handler $err] $reg]))
		
		((" function) (" continuation)
			(if (not [= [length $d] #4])
				{$handler (set-storage $err special-form-error $d (null))}
			(if (not [i/f:symbol? [@cadr $d]])
				{$handler (set-storage $err special-form-error $d [@cadr $d])}
			(if [i/f:symbol? [@caddr $d]]
				{$handler (set-storage $err special-form-error $d [@caddr $d])}
				(with return (let (parameters nil) (begin
					(foreach (v [@caddr $d])
						(if [i/f:symbol? $v]
							[append [build-expression $v $reg $handler $err] parameters $reg]
							{$handler (set-storage $err special-form-error $d $v)}))
					{return [(switch str= [to-string [@car $d] $reg]
							((" function) make-function)
							make-continuation)
						[build-expression [@cadr $d] $reg $handler $err] $parameters [build-expression [@cadddr $d] $reg $handler $err]
							$reg]})))))))
		
		((" invoke) (" jump) (" storage)
			(if [= [length $d] #1]
				{$handler (set-storage $err special-form-error $d (null))}
			(if (and [str= [to-string [@car $d] $reg] (" storage)] (not [i/f:symbol? [@cadr $d]]))
				{$handler (set-storage $err special-form-error $d [@cadr $d])}
				(with return (let (arguments nil) (begin
					(foreach (v [@cddr $d]) [append [build-expression $v $reg $handler $err] arguments $reg])
					{return [(switch str= [to-string [@car $d] $reg]
							((" invoke) make-invoke)
							((" jump) make-jump)
							make-storage)
						[build-expression [@cadr $d] $reg $handler $err] $arguments $reg]}))))))
		
		((" literal)
			(if (not [= [length $d] #2])
				{$handler (set-storage $err special-form-error $d (null))}
			(if (or (not [i/f:symbol? [@cadr $d]]) (not [= [length [@cadr $d]] (build-expression-literal-len)]))
				{$handler (set-storage $err special-form-error $d [@cadr $d])}
				[make-literal (with return
					{(continuation loop (str i value)
							(if [= $i #0]
								{return $value}
							(if [= [getb $str] (char 1)]
								{loop [+ $str #1] [- $i #1] [+ [<< $value #1] #1]}
							(if [= [getb $str] (char 0)]
								{loop [+ $str #1] [- $i #1] [<< $value #1]}
								{$handler (set-storage $err special-form-error $d [@cadr $d])}))))
						[to-string [@cadr $d] $reg] (build-expression-literal-len) #0}) $reg])))
		
		[make-meta [build-expression [@car $d] $reg $handler $err] [@cdr $d] $reg]))))
